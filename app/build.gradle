apply plugin: 'com.android.application'

def releaseTime() {
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC"))
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    defaultConfig {
        applicationId "com.tryking.EasyList"
        minSdkVersion 14
        targetSdkVersion 22
        versionCode 1
        versionName "1.0.0"

        // dex突破65535的限制
        multiDexEnabled true
    }

    signingConfigs {
        debug {
            // No debug config
        }

        release {

        }
    }


    buildTypes {
        release {
            // 不显示Log
            buildConfigField "boolean", "LOG_DEBUG", "false"
            //debug标记
            manifestPlaceholders = [IS_DEBUG: "no"]

            //混淆(混淆8000多错误但是能运行)
            minifyEnabled true
            zipAlignEnabled true
            // 移除无用的resource文件（必须混淆才有效)
            shrinkResources true

            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release

            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    def outputFile = output.outputFile
                    if (outputFile != null && outputFile.name.endsWith('.apk')) {
                        // 输出apk名称为qingdaner_v1.0_2016-01-15_wandoujia.apk
                        def fileName = "qingdaner_v${defaultConfig.versionName}_${releaseTime()}_${variant.productFlavors[0].name}.apk"
                        output.outputFile = new File(outputFile.parent, fileName)
                    }
                }
            }
        }

        debug {
            signingConfig signingConfigs.release

            //debug标记
            manifestPlaceholders = [IS_DEBUG: "yes"]
        }
    }

    // 友盟多渠道打包
    productFlavors {
        _Debug {}
        wandoujia {}
        _360 {}
        baidu {}
        xiaomi {}
        tencent {}
        taobao {}
        huawei {}
        lianzhihong {}
        alibabaMianshi {}
    }

    productFlavors.all { flavor ->
        flavor.manifestPlaceholders = [UMENG_CHANNEL_VALUE: name]
    }

    repositories {
        //仓库：Flat directory,Maven,lvy(Studio支持这三种)
        maven {
            url "https://jitpack.io"
        }

        mavenCentral()//Glide

        //友盟反馈
        jcenter()
        maven { url "https://raw.githubusercontent.com/umeng/mvn-repo-umeng/master/repository" }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile 'junit:junit:4.12'
    /** ****友盟社会化分享 *******/
    compile files('libs/umeng_social_sdk.jar')
    //QQ
    compile files('libs/mta-sdk-1.6.2.jar')
    compile files('libs/open_sdk_r5756.jar')
    compile files('libs/SocialSDK_QQZone_3.jar')
    //新浪微博
    compile files('libs/SocialSDK_Sina.jar')
    compile files('libs/weiboSDKCore_3.1.4.jar')
    //微信
    compile files('libs/SocialSDK_WeiXin_2.jar')
    compile files('libs/SocialSDK_WeiXin_1.jar')
    /** ***************************/

    compile 'com.android.support:appcompat-v7:23.3.0'
    compile 'com.android.support:design:23.3.0'
    compile 'com.android.support:cardview-v7:23.3.0'
    compile 'com.google.code.gson:gson:2.3.1'
    compile 'com.orhanobut:logger:1.11'
    compile 'com.jakewharton:butterknife:7.0.1'
    compile 'com.android.support:support-v4:23.3.0'
    //数据库，暂时用不到
//    compile 'com.j256.ormlite:ormlite-android:4.48'
    compile 'com.mcxiaoke.volley:library:1.0.19'
    //Fresco用不到，jar包太大，换Glide
//    compile 'com.facebook.fresco:fresco:0.11.0'
    /* 日历选择器(暂时用不到) */
//    compile 'com.prolificinteractive:material-calendarview:1.4.0'
    /*  图表  */
    compile 'com.github.PhilJay:MPAndroidChart:v3.0.0-beta1'
    compile 'com.github.bumptech.glide:glide:3.7.0'
    //    compile 'com.android.support:multidex:1.0.1'
    /* 可展开RecyclerView */
    compile('com.h6ah4i.android.widget.advrecyclerview:advrecyclerview:0.9.3@aar') {
        transitive = true
    }
    /* U盟统计 */
    compile 'com.umeng.analytics:analytics:latest.integration'
    /* 友盟反馈 */
    compile 'com.umeng:fb:5.4.0'
}
